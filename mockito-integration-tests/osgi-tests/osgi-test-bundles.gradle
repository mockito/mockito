buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath libs.bnd.gradle
    }
}

// Test bundles for OSGi tests

sourceSets {
    testBundle
    otherBundle
}

dependencies {
    testBundleImplementation project(":mockito-core")
    testBundleImplementation libs.junit4
    testBundleImplementation sourceSets.otherBundle.output
}

import aQute.bnd.gradle.Bundle

task testBundle(type: Bundle) {
    from sourceSets.testBundle.output
}

tasks.register('otherBundle', Bundle) {
    from sourceSets.otherBundle.output
}

tasks.withType(Bundle).configureEach {
    archiveAppendix = name
    bundle { // this: BundleTaskExtension
        bnd = """
            Bundle-SymbolicName: $name
            -exportcontents: *
        """.stripIndent()
    }
}
