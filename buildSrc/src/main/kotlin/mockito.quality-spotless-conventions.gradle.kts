import com.diffplug.gradle.spotless.SpotlessExtension
import com.google.googlejavaformat.java.Formatter
import com.google.googlejavaformat.java.JavaFormatterOptions

plugins {
    id("com.diffplug.spotless")
}

configure<SpotlessExtension> {
    // We run the check separately on CI, so don't run this by default
    isEnforceCheck = false

    java {
        licenseHeaderFile(rootProject.file("config/spotless/spotless.header"))

        // not using stock googleJavaFormat due to missing options like reorderModifiers
        custom("google-java-format") {
            val formatterOptions = JavaFormatterOptions.builder()
                .style(JavaFormatterOptions.Style.AOSP)
                .reorderModifiers(true)
                .formatJavadoc(false)
                .build()

            Formatter(formatterOptions).formatSource(it)
        }

        toggleOffOn()
    }

    format("misc") {
        target(
            "*.gradle",
            "*.gradle.kts",
            ".gitignore",
        )
        targetExclude("**/build/")
        trimTrailingWhitespace()
    }
}

