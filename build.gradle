buildscript {
    repositories {
        mavenLocal() //for local testing of mockito-release-tools
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0' // later versions don't work on JDK6
        classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.3.0' //for 'java6-compatibility.gradle'

        //Needed so that release notes and release workflow plugin can be applied in separate gradle/*.gradle build files
        classpath project.'dependencies.mockito-release-tools'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.0'
}

description = 'Mockito mock objects library core API and implementation'

apply plugin: "org.mockito.mockito-release-tools.continuous-delivery"

apply from: 'gradle/root/ide.gradle'
apply from: 'gradle/root/gradle-fix.gradle'
apply from: 'gradle/root/java6-compatibility.gradle'
apply from: 'gradle/java-library.gradle'
apply from: 'gradle/root/coverage.gradle'

apply from: 'gradle/mockito-core/inline-mock.gradle'
apply from: 'gradle/mockito-core/osgi.gradle'
apply from: 'gradle/mockito-core/javadoc.gradle'
apply from: 'gradle/mockito-core/license.gradle'
apply from: 'gradle/mockito-core/testing.gradle'

apply from: "gradle/dependencies.gradle"

archivesBaseName = "mockito-core"

allprojects {
    repositories {
        jcenter()
    }
    tasks.withType(JavaCompile) {
        //I don't believe those warnings add value given modern IDEs
        options.warnings = false
        options.encoding = 'UTF-8'
    }
    apply plugin: 'checkstyle'
    checkstyle {
       configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }
}

configurations {
    testUtil //TODO move to separate project
}

dependencies {
    compile 'net.bytebuddy:byte-buddy:1.6.14', 'net.bytebuddy:byte-buddy-agent:1.6.14'

    compileOnly libraries.junit4, "org.hamcrest:hamcrest-core:1.3"
    compile "org.objenesis:objenesis:2.5"

    testCompile 'org.ow2.asm:asm:5.2'

    testCompile libraries.assertj

    //putting 'provided' dependencies on test compile and runtime classpath
    testCompileOnly configurations.compileOnly
    testRuntime configurations.compileOnly

    testUtil sourceSets.test.output
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

//Posting Build scans to https://scans.gradle.com
buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}
