import org.apache.xalan.xsltc.cmdline.Compile

Properties props = new Properties()
props.load(new FileInputStream("${rootProject.projectDir}/version.properties"))

version = props.version
group = 'org.mockito'
description = 'Core API and implementation.'

allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
    }
}

// EXPERIMENTAL : coverage
// see https://github.com/kt3k/coveralls-gradle-plugin
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.0.0' // coveralls plugin depends on cobertura plugin
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.1.6'
    }
}

sourceCompatibility = 1.5

configurations {
    provided
    testUtil //TODO move to separate project
}

sourceSets {
    main {
        java.srcDir 'src'
        compileClasspath = compileClasspath + configurations.provided
    }
    test {
        java.srcDir 'test'
        compileClasspath = compileClasspath + configurations.provided
    }
}

test.include "**/*Test.class"

tasks.withType(Compile) {
    options.warnings = false
}

dependencies {
    provided fileTree('lib/compile')
    compile fileTree('lib/run') { exclude '*.txt' }, fileTree('lib/repackaged') { exclude '*.txt'}

    testCompile fileTree("lib/test")
    testRuntime fileTree("lib/compile")

    testUtil sourceSets.test.output

    compile 'org.objenesis:objenesis:2.1'
    compile 'org.hamcrest:hamcrest-core:1.1'

}

// Configure VCS in generated IPR file
idea.project.ipr {
    withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
    }
}

idea.module.scopes.PROVIDED.plus += configurations.provided
eclipse.classpath.plusConfigurations += configurations.provided


// EXPERIMENTAL : coverage
// configure code coverage
apply plugin: 'cobertura'
apply plugin: 'coveralls'
cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report
//cobertura.coverageIgnoreTrivial = true



def commonJarContent = copySpec {
    //source
    from(sourceSets.main.allSource)

    //mockito license
    from(".") { include 'LICENSE', 'NOTICE' }

    //repackaged license
    from("lib/repackaged") { exclude '**/*.jar' }
    //repackaged classes
    from(zipTree("lib/repackaged/cglib-and-asm-1.0.jar")) {
        exclude 'META-INF/MANIFEST.MF'
    }
}

jar {
    baseName = 'mockito-core'
    with commonJarContent
}

task allJar(type: Jar) {
    baseName = 'mockito-all'

    with commonJarContent

    //classes
    from(sourceSets.main.output)

    //3rd party license files
    from("lib/run") { exclude '**/*.jar' }
}

tasks.withType(Jar) { task ->
    doLast {
        project.exec {
            commandLine 'ant', '-f', 'build-ant.xml', "osgify.$task.baseName"
        }
    }
}

artifacts {
    archives allJar
}

/*
repositories {
    mavenCentral()
}

apply plugin: 'pmd'

pmd {
    ruleSetFiles = files('conf/pmd-rules.xml')
}
*/

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
