buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2' //publishing to bintray
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.2' //rest calls to bintray api
    }
}
apply plugin: 'java'
group = 'org.mockito'
description = 'Core API and implementation.'
sourceCompatibility = 1.6
targetCompatibility = 1.6

apply plugin: 'maven-publish'

apply from: 'gradle/version.gradle'
apply from: "gradle/ide.gradle"
apply from: 'gradle/coverage.gradle'
apply from: 'gradle/osgi.gradle'
apply from: 'gradle/gradle-fix.gradle'

allprojects {
    repositories {
        jcenter()
    }
    tasks.withType(JavaCompile) {
        //I don't believe those warnings add value given modern IDEs
        options.warnings = false
    }
}

configurations {
    provided
    testUtil //TODO move to separate project
}

sourceSets {
    main {
        compileClasspath = compileClasspath + configurations.provided
    }
    test {
        compileClasspath = compileClasspath + configurations.provided
    }
}

test {
    include "**/*Test.class"
    testLogging {
        exceptionFormat 'full'
        showCauses true
    }
}

dependencies {
    compile 'net.bytebuddy:byte-buddy:1.4.17'

    provided "junit:junit:4.12", "org.hamcrest:hamcrest-core:1.3"
    compile "org.objenesis:objenesis:2.4"

    testCompile 'org.ow2.asm:asm:5.1'

    testCompile 'org.assertj:assertj-core:1.7.1'

    testRuntime configurations.provided

    testUtil sourceSets.test.output
}

def licenseFiles = copySpec {
    //mockito license
    from(".") { include 'LICENSE' }
}

task sourcesJar(type: Jar) {
    jar {
        baseName = 'mockito-core'
        from(sourceSets.main.allSource)
        with licenseFiles

    }
    baseName = 'mockito-core'
    from(sourceSets.main.allSource)
    classifier = "sources"
    with licenseFiles
}

apply from: 'gradle/javadoc.gradle'
task javadocJar(type: Jar) {
    baseName = 'mockito-core'
    classifier = "javadoc"
    with licenseFiles
    from mockitoJavadoc
}

task coverageReport(dependsOn: ["test", "mockitoCoverage"]) {}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mockitoCore(MavenPublication) {
            from components.java
            artifactId 'mockito-core'
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

apply from: 'gradle/release.gradle'
apply from: "gradle/pom.gradle"

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

task ciBuild {
    //validate the state of the project
    dependsOn { allprojects*.build }
    dependsOn publishToMavenLocal, tasks.idea, tasks.eclipse
}

//Making sure that release task is only invoked after the entire ciBuild validation
release.mustRunAfter ciBuild
