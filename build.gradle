buildscript {
    repositories {
        mavenLocal() //for local testing of mockito-release-tools
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:4.0.1'

        classpath "io.github.gradle-nexus:publish-plugin:2.0.0-rc-1"
        classpath 'org.shipkit:shipkit-changelog:2.0.1'
        classpath 'org.shipkit:shipkit-auto-version:2.0.11'

        classpath 'com.google.googlejavaformat:google-java-format:1.24.0'
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.24"
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.25.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

apply from: "$rootDir/gradle/shipkit.gradle"
apply from: "$rootDir/gradle/root/ide.gradle"
apply from: "$rootDir/gradle/license.gradle"

subprojects { proj ->
    repositories {
        mavenCentral()
        google()
    }
    plugins.withId('java') {
        proj.apply from: "$rootDir/gradle/errorprone.gradle"
        proj.apply from: "$rootDir/gradle/jfr.gradle"
        proj.apply from: "$rootDir/gradle/spotless.gradle"
    }
    tasks.withType(JavaCompile).configureEach {
        //I don't believe those warnings add value given modern IDEs
        options.warnings = false
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
        options.setSource('11')
    }

    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        dirMode = Integer.parseInt("0755", 8)
        fileMode = Integer.parseInt("0644", 8)
    }

    apply plugin: 'checkstyle'
    checkstyle {
       configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }
}
