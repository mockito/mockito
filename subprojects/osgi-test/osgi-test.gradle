plugins {
    id 'java'
}

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "osgi-test-bundles.gradle"

description = "Test suite for OSGi framework with Mockito"

dependencies {
    testCompile libraries.junit4
    testCompile libraries.osgi

    testRuntime libraries.equinox
}

tasks.javadoc.enabled = false

configurations {
    testRuntimeBundles
}

dependencies {
    testRuntimeBundles project.rootProject
    testRuntimeBundles libraries.bytebuddy
    testRuntimeBundles libraries.objenesis3
    testRuntimeBundles tasks.testBundle.outputs.files
    testRuntimeBundles tasks.otherBundle.outputs.files
}

test {
    dependsOn configurations.testRuntimeBundles
    inputs.files(sourceSets.testBundle.allSource)
    inputs.files(sourceSets.otherBundle.allSource)
    systemProperty 'testRuntimeBundles', configurations.testRuntimeBundles.asPath
    useJUnit()
}

