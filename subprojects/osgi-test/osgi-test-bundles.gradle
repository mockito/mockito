buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath libraries.bndGradle
    }
}

apply from: "$rootDir/gradle/dependencies.gradle"

description = "Test bundles for OSGi tests"

sourceSets {
    testBundle
    otherBundle
}

dependencies {
    testBundleImplementation project.rootProject
    testBundleImplementation libraries.junit4
    testBundleImplementation sourceSets.otherBundle.output
}

import aQute.bnd.gradle.Bundle

task testBundle(type: Bundle) {
    from sourceSets.testBundle.output
}

task otherBundle(type: Bundle) {
    from sourceSets.otherBundle.output
}

tasks.withType(Bundle) {
    archiveAppendix = name
    bnd = """
Bundle-SymbolicName: $name
-exportcontents: *
"""
}
