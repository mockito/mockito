apply plugin: 'java'
apply plugin: 'maven'
description = "Mockito for TestNG"

//TODO SF ensure this one is released, too (ensure source and target compatibility, too).

Properties props = new Properties()
props.load(new FileInputStream(rootProject.file("version.properties")))

group = 'org.mockito'
archivesBaseName = 'mockito-testng'
version = props['mockito.testng.version']

jar {
    manifest.attributes["Created-By"] =
            "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version

    from("${rootProject.projectDir}") {
        include "LICENSE"
        into "META-INF"
        expand(copyright: new Date().format("yyyy"), version: project.version)
    }

}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = "sources"
    from sourceSets.main.allJava.srcDirs
    include "**/*.java", "**/*.aj"
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${rootProject.projectDir}/target/maven/repository")
        }
    }
}

dependencies {
    compile project.rootProject
    compile 'org.testng:testng:6.3.1'
    testCompile 'org.assertj:assertj-core:1.7.1'
}

test {
    useTestNG() {
        suites file('src/test/resources/mockito-testng.xml')
    }
}