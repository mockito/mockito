description = "Mockito for TestNG"

Properties props = new Properties()
props.load(new FileInputStream("${rootProject.projectDir}/version.properties"))

group = 'org.mockito'
archivesBaseName = 'mockito-testng'
version = props['mockito.testng.version']

repositories {
    mavenCentral()
}

jar {
    manifest.attributes["Created-By"] =
            "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version

    from("${rootProject.projectDir}") {
        include "LICENSE"
        include "NOTICE"
        into "META-INF"
        expand(copyright: new Date().format("yyyy"), version: project.version)
    }

}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = "sources"
    from sourceSets.main.allJava.srcDirs
    include "**/*.java", "**/*.aj"
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${rootProject.projectDir}/target/maven/repository")
        }
    }
}

dependencies {
    compile project.rootProject
    compile 'org.testng:testng:6.3.1'
    testCompile 'org.easytesting:fest-assert:1.4'
}

test {
    useTestNG() {
        suites file('src/test/resources/mockito-testng.xml')
    }
}