apply plugin: 'osgi'

afterEvaluate {
    tasks.matching { it.name == 'jar' }.all { jar ->
        jar.manifest {
            classpath = project.configurations.runtime

            instruction '-versionpolicy', '[${version;==;${@}},${version;+;${@}})'

            name = 'Mockito Mock Library for Java. Core bundle requires Byte Buddy and Objenesis.'
            symbolicName = 'org.mockito.mockito-core'
            version = project.version.replace('-', '.')

            instruction 'Export-Package',
                    '!org.mockito.internal.*',
                    "org.mockito.*;version=${version}"

            instruction 'Import-Package',
                    'net.bytebuddy.*;version="[1.6.0,2.0)"',
                    'junit.*;resolution:=optional',
                    'org.junit.*;resolution:=optional',
                    'org.hamcrest;resolution:=optional',
                    'org.objenesis;version="[2.5,3.0)"',
                    'org.mockito.*'

            instruction 'Private-Package',
                    'org.mockito.*'

            instruction '-removeheaders', 'Private-Package'

            attributes 'Automatic-Module-Name': 'org.mockito'
        }
    }
}
