apply plugin: 'com.jfrog.bintray'

//if the build fails, we don't want to publish
tasks.bintrayUpload.mustRunAfter "build"

def theVcsTag = "v" + project.version

assert project.hasProperty("mavenCentralSync")     : "Please configure 'ext.mavenCentralSync' for project that wants to use publishing"
assert project.hasProperty("bintrayAutoPublish")  : "Please configure 'ext.bintrayAutoPublish' for project that wants to use publishing"
assert project.description  : "Please configure 'description' for project that wants to use publishing"

bintray {
    user = 'szczepiq'
    key = System.env.MOCKITO_BINTRAY_API_KEY

    //Workaround for bintray plugin/Gradle bug (https://github.com/bintray/gradle-bintray-plugin/issues/159)
    if (!('tasks' in gradle.startParameter.taskNames)) {
        publications = project.publishing.publications*.name
    }

    publish = project.ext.bintrayAutoPublish

    pkg {
        repo = project.ext.bintrayRepo

        userOrg = 'mockito'
        // so that despite using 'szczepiq' user, the artifact is published
        // to 'mockito' organization and all members can modify/edit it

        name = 'mockito'
        desc = project.description
        websiteUrl = 'http://mockito.org'
        issueTrackerUrl = 'https://github.com/mockito/mockito/issues'
        vcsUrl = 'https://github.com/mockito/mockito.git'
        licenses = ['MIT']
        labels = ['mocks', 'tdd', 'unit tests']
        publicDownloadNumbers = true

        // optional version attributes
        version {
            vcsTag = theVcsTag

            gpg {
               sign = true
            }

            //Automatically syncs to central repository (https://oss.sonatype.org/)
            mavenCentralSync {
                sync = project.ext.mavenCentralSync
                user = System.env.NEXUS_TOKEN_USER
                password = System.env.NEXUS_TOKEN_PWD
            }
        }
    }
}