apply plugin: "org.mockito.mockito-release-tools.continuous-delivery"

releasing {
    gitHub.repository = "mockito/mockito"
    gitHub.readOnlyAuthToken = "a0a4c0f41c200f7c653323014d6a72a127764e17"
    //TODO document why we are using real user name here and for Bintray user, too
    gitHub.writeAuthUser = "szczepiq"
    gitHub.writeAuthToken = System.getenv("GH_WRITE_TOKEN")

    releaseNotes.file = "doc/release-notes/official.md"
    releaseNotes.notableFile = "doc/release-notes/notable.md"
    releaseNotes.labelMapping = [
        '1.* incompatible': 'Incompatible changes with previous major version (v1.x)',
        'java-9': "Java 9 support",
        'java-8': "Java 8 support",
        'new feature': "New features",
        'BDD': 'Behavior-Driven Development support',
        'bug': "Bugfixes",
        'enhancement': "Enhancements",
        'android': "Android support",
        'docs': 'Documentation'
    ]

    git.user = "Mockito Release Tools"
    git.email = "<mockito.release.tools@gmail.com>"
    //git.releasableBranchRegex = "master|release/.+"  // matches 'master', 'release/2.x', 'release/3.x', etc.
    git.releasableBranchRegex = "release/.+"  // 'release/2.x', 'release/3.x', etc.

    team.developers = ['szczepiq:Szczepan Faber', 'bric3:Brice Dutheil', 'raphw:Rafael Winterhalter', 'TimvdLippe:Tim van der Lippe']
    team.contributors = []
}

allprojects {
    plugins.withId("org.mockito.mockito-release-tools.bintray") {
        bintray {
            key = System.getenv("BINTRAY_API_KEY")
            pkg {
                repo = 'maven' //https://bintray.com/mockito/maven
                user = 'szczepiq' //https://bintray.com/szczepiq
                userOrg = 'mockito' //https://bintray.com/mockito
                name = 'mockito' //https://bintray.com/mockito/maven/mockito
                licenses = ['MIT']
                labels = ['mocks', 'tdd', 'unit tests']
                publish = true //can be changed to 'false' for testing

                //See our Bintray packages at https://bintray.com/mockito/maven
                name = rootProject.ext.has('release_notable')? "mockito" : "mockito-development"

                version {
                    //Automatically syncs to central repository (https://oss.sonatype.org/)
                    mavenCentralSync {
                        sync = false
                        // TODO, change above to below when the new publication logic is rid of glitches:
                        // sync = rootProject.ext.has('release_notable') //can be set to 'false' for testing
                        user = System.env.NEXUS_TOKEN_USER
                        password = System.env.NEXUS_TOKEN_PWD
                    }
                }
            }
        }
    }
}
