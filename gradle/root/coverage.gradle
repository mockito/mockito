apply plugin: 'jacoco'

// TODO later include subprojects, possible ideas https://gist.github.com/aalmiray/e6f54aa4b3803be0bcac

jacoco {
    toolVersion = "0.7.8"
}

//The tests for the new plugin mechanism are broken with Jacoco 0.7.8 - https://github.com/mockito/mockito/issues/860
test {
    jacoco {
        excludes = ['org.mockitoutil.ClassLoadersTest**', 'org.mockito.internal.configuration.GlobalConfigurationTest**']
    }
}

task mockitoCoverage(type: JacocoReport, dependsOn: "test") {
    executionData files("${buildDir}/jacoco/test.exec")

    sourceSets sourceSets.main

    reports {
        xml.enabled true
        html.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }

    afterEvaluate {
      classDirectories = files(classDirectories.files.collect {
        fileTree(
          dir: it,
          exclude: ['**/org/mockito/internal/util/concurrent/**']
        )
      })
    }

    doLast {
        println "Jacoco report for client created: file://${reports.xml.destination.toURI().path}"
    }
}

task coverageReport(dependsOn: ["test", "mockitoCoverage"]) {}
